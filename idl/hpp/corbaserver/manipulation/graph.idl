// Copyright (c) 2014, LAAS-CNRS
// Authors: Joseph Mirabel (joseph.mirabel@laas.fr)
//
// This file is part of hpp-manipulation.
// hpp-manipulation is free software: you can redistribute it
// and/or modify it under the terms of the GNU Lesser General Public
// License as published by the Free Software Foundation, either version
// 3 of the License, or (at your option) any later version.
//
// hpp-manipulation is distributed in the hope that it will be
// useful, but WITHOUT ANY WARRANTY; without even the implied warranty
// of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
// General Lesser Public License for more details.  You should have
// received a copy of the GNU Lesser General Public License along with
// hpp-manipulation. If not, see <http://www.gnu.org/licenses/>.


#ifndef HPP_MANIPULATION_CORBA_GRAPH_IDL
# define HPP_MANIPULATION_CORBA_GRAPH_IDL

# include "hpp/corbaserver/manipulation/gcommon.idl"
# include <hpp/corbaserver/common.idl>

module hpp {
  module corbaserver {
    module manipulation {
      interface Graph {
        /// Initialize the graph of constraints.
        /// \note The composite hpp::manipulation::robot must be completely defined first.
        long createGraph(in string graphName)
          raises (Error);

        /// Create a subgraph of the constraint graph for one particular end-effector.
        long createSubGraph(in string subgraphName)
          raises (Error);

        /// Add a node to the graph.
        /// \param subGraphId is the ID of the subgraph to which the node should be added.
        /// \param nodeName the name of the new node.
        long createNode (in long subGraphId, in string nodeName)
          raises (Error);

        /// Add an edge between two nodes of the graph.
        /// \param nodeFromId, nodeToId the ID of the ends of the new edge.
        /// \param edgeName name of the new edge.
        /// \param weight weight of the edge.
        long createEdge (in long nodeFromId, in long nodeToId, in string edgeName, in long weight, in boolean isInNodeFrom)
          raises (Error);

        /// Add an edge with waypoint between two nodes of the graph.
        /// \param nodeFromId, nodeToId the ID of the ends of the new edge.
        /// \param edgeName name of the new edge.
        /// \param weight weight of the edge.
        long createWaypointEdge (in long nodeFromId, in long nodeToId, in string edgeName, in long weight, in boolean isInNodeFrom)
          raises (Error);

        /// Get waypoint IDs of an edge.
        /// \param edgeId the ID of the edge.
        /// \return the ID of the edge and the node associated with the node.
        long getWaypoint (in long edgeId, out ID nodeId)
          raises (Error);

        /// Add an edge of type LevelSetEdge between two nodes.
        /// \param nodeFromId, nodeToId the ID of the ends of the new edge.
        /// \param edgeName name of the new edge.
        /// \param weight weight of the edge.
        long createLevelSetEdge(in long nodeFromId, in long nodeToId, in string edgeName, in long weight, in boolean isInNodeFrom)
          raises (Error);

        /// Set the numerical constraints of a LevelSetEdge that are create the foliation.
        /// \param graphComponentId ID of the component.
        /// \param numericalConstraintNames is an array of names of numerical constraints in the ProblemSolver map.
        /// \param lockedDofNames is an array of names of LockedDof constraints in the ProblemSolver map.
        void setLevelSetConstraints (in long edgeId, in Names_t numericalConstraintNames, in Names_t lockedDofNames)
          raises (Error);

        /// Set the numerical constraints of a component.
        /// \param graphComponentId ID of the component.
        /// \param constraintNames is an array of names of constraints in the ProblemSolver map.
        void setNumericalConstraints (in long graphComponentId, in Names_t constraintNames)
          raises (Error);

        /// Set the numerical constraints for path of a node.
        /// \param nodeId ID of the node.
        /// \param constraintNames is an array of names of constraints in the ProblemSolver map.
        void setNumericalConstraintsForPath (in long nodeId, in Names_t constraintNames)
          raises (Error);

        /// Set the LockedDof constraints of a component.
        /// \param graphComponentId ID of the component.
        /// \param constraintNames is an array of names of constraints in the ProblemSolver map.
        void setLockedDofConstraints (in long graphComponentId, in Names_t constraintNames)
          raises (Error);

        /// Activate statistic analysis for this sequence of edges.
        /// \param IDedge edge ID.
        void statOnConstraint (in ID IDedge)
          raises (Error);

        /// Get the node corresponding to the state of the configuration.
        /// \param dofArray the configuration.
        /// \return the ID corresponding to the node.
        void getNode (in floatSeq dofArray, out ID nodeId)
          raises (Error);

        void display ()
          raises (Error);
      }; // interface Graph
    }; // module manipulation
  }; // module corbaserver
}; // module hpp

#endif // HPP_MANIPULATION_CORBA_GRAPH_IDL
